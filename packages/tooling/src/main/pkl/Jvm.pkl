/// Describes KJVM configurations for an Elide project.
@ModuleInfo { minPklVersion = "0.28.1" }
module elide.jvm

import "Base.pkl" as common
import "Artifacts.pkl" as artifacts
import "Sources.pkl" as sources

/// JVM target levels as integers.
typealias JvmTargetLevel = "latest" | "stable" | "auto" | Float(1.8) | Float(1.9) | Int8(isBetween(8, 29))

/// JVM target levels as enumeration.
typealias JvmTarget = JvmTargetLevel

/// Maven group string.
typealias MavenGroup = String

/// Maven module string.
typealias MavenModule = String

/// Matches against Maven coordinate strings.
typealias MavenCoordinate = String(matches(
    Regex(#"^[a-zA-Z0-9_\-.]+(\.[a-zA-Z0-9_\-.]+)*:[a-zA-Z0-9_\-.]+(\:[a-zA-Z0-9_\-.]+)?$"#)
))

/// Known standard JAR manifest keys.
typealias StandardManifestKey = "Implementation-Title" | "Implementation-Version" | "Implementation-Vendor" |
    "Implementation-URL" | "Implementation-Description" | "Implementation-Build-Id" |
    "Implementation-Build-Time" | "Implementation-Build-Host" | "Implementation-Build-User" |
    "Class-Path" | "Main-Class" | "Bundle-SymbolicName" | "Bundle-Version"

/// Key within a JAR manifest.
typealias JarManifestKey = StandardManifestKey | String

/// Value within a JAR manifest.
typealias JarManifestValue = String

/// Class name for a JVM class, which is a string.
typealias JvmClass = String

/// Whether to default to using compression for JARs.
const defaultUseJarCompression = true

/// Options which apply to JARs.
class JarOptions {
  /// Whether to apply compression.
  compress: Boolean = defaultUseJarCompression

  /// Whether to add default manifest properties.
  defaultManifestProperties: Boolean = true

  /// Main entrypoint for the JAR, if applicable.
  entrypoint: String?
}

/// Abstract definition of a JAR resource.
abstract class JarResource {
  /// File path to mount this resource at within the JAR.
  path: common.FilePath
}

/// Defines one or more JAR resources from files.
class FilesJarResource extends JarResource {
  /// Path to the file to embed as a JAR resource.
  files: common.FilePath
}

/// Defines one or more JAR resources from an artifact.
class ArtifactJarResource extends JarResource {
  /// Artifact to depend on and embed.
  artifact: artifacts.ArtifactName
}

/// Describes a JAR output artifact.
open class Jar extends artifacts.Artifact {
  /// Filename for the resulting JAR.
  name: common.FileName?

  /// Which source set to build this JAR from.
  sources: Listing<sources.SourceSetName> = new {
    default {
      "main"
    }
  }

  /// Which resources to add to the JAR.
  resources: Listing<common.FilePath | JarResource> = new {}

  /// Keys and values to include in the JAR's manifest.
  manifest: Mapping<JarManifestKey, JarManifestValue> = new {}

  /// Options for the JAR.
  options: JarOptions = new {}
}

/// Describes configuration for a shaded JAR.
open class ShadedJar extends Jar {}

/// Base coordinate specification for a Maven package.
open class MavenCoordinateSpec {
  /// Group for the coordinate.
  group: MavenGroup?

  /// Name for the coordinate.
  name: MavenModule?

  /// Classifier for this Maven coordinate.
  classifier: String?
}

/// Describes a Maven package dependency.
class MavenPackageSpec extends MavenCoordinateSpec {
  /// Group for the package.
  group: MavenGroup?

  /// Name for the package.
  name: MavenModule?

  /// Version or symbolic version for the package.
  version: common.PackageVersion?

  /// Full Maven coordinate for the package; if specified, this is preferred to the group and name.
  coordinate: MavenCoordinate?

  /// Name of a custom repository to pull this dependency from.
  repository: common.RepositoryName?
}

/// Describes a Maven repository to include when resolving dependencies.
class MavenRepositorySpec {
  /// URL where this repository can be accessed.
  url: common.RepositoryUrl?

  /// File path to a local repository.
  path: common.FilePath?
}

/// Defines a Gradle catalog and settings.
class GradleCatalogSpec {
  /// Path to the Gradle dependency catalog.
  path: common.FilePath
}

/// Describes a Gradle catalog, which can be specified strucuturally, or as a simple path.
typealias GradleCatalog = GradleCatalogSpec | common.FilePath

/// Maven packages can be specified structurally, or as coordinate strings, Gradle-style coordinate strings, or purls.
typealias MavenPackageDependency = MavenPackageSpec | MavenCoordinate | common.Purl

/// Specifies a simple Maven repository endpoint or structural definition.
typealias MavenRepository = MavenRepositorySpec | common.RepositoryUrl


// Listing type for Maven dependencies with a default element shape. Ensures `new {}`
// within listings yields a `MavenPackageSpec` even when the listing is overridden.
/// Coordinates for this project as a library.
class MavenLibraryCoordinate extends MavenCoordinateSpec

/// Configuration for Maven dependency resolution.
class MavenDependencies {
  /// Coordinates for this project as a library, if applicable.
  coordinates: MavenLibraryCoordinate?

  /// A list of Maven package dependencies to be resolved for this project.
  packages: Listing<MavenPackageDependency> = new Listing<MavenPackageDependency> {
    // Because MavenPackageDependency is a union type, Pkl cannot infer the default
    // shape for `new {}` entries. Declare an explicit default so `new {}` yields a
    // MavenPackageSpec element.
    default = (index) -> new MavenPackageSpec {}
  }

  /// A list of Maven package dependencies to be resolved as processor dependencies.
  processors: Listing<MavenPackageDependency> = new Listing<MavenPackageDependency> {
    default = (index) -> new MavenPackageSpec {}
  }

  /// A list of Maven package dependencies to be resolved for this project.
  testPackages: Listing<MavenPackageDependency> = new Listing<MavenPackageDependency> {
    default = (index) -> new MavenPackageSpec {}
  }

  /// A list of Gradle dependency catalogs to be resolved for this project.
  catalogs: Listing<GradleCatalog> = new {}

  /// A suite of extra Maven repositories.
  repositories: Mapping<String, MavenRepository> = new {}

  /// Whether to enable default repositories like Maven Central. Defaults to `true`.
  enableDefaultRepositories: Boolean = true

  /// Foreign manifest(s) to import (for example, POM files).
  from: String | Listing<common.FilePath> = new Listing<common.FilePath> {}
}

/// Create a JAR artifact from the `main` project source set.
function jar(jarName: String) = new Jar {
  name = "\(jarName.replaceAll(".jar", "")).jar"
}

/// Add a source set to a JAR artifact.
function jarSources(sourceSetName: sources.SourceSetName) = new Mixin<Jar> {
  sources {
    sourceSetName
  }
}

/// Add a resource to a JAR artifact.
function jarResources(_file: common.FilePath, at: common.FilePath) = new Mixin<Jar> {
  resources {
    new FilesJarResource {
      files = _file
      path = at
    }
  }
}

/// Embed an artifact within a JAR.
function withArtifact(artifactName: artifacts.ArtifactName, at: common.FilePath) = new Mixin<Jar> {
  artifacts {
    artifactName
  }
  resources {
    new ArtifactJarResource {
      artifact = artifactName
      path = at
    }
  }
}

/// Create a JAR artifact from the `main` project source set.
function app(_entrypoint: common.FilePath | String) = new Jar {
  name = "main.jar"
  options {
    entrypoint = _entrypoint
  }
  manifest {
    ["Main-Class"] = _entrypoint
  }
}

/// Controls features related to JVM support in Elide projects.
class JvmFeatures {
  /// Automatically provide test dependencies for JVM projects, like JUnit.
  testing: Boolean = true
}

/// Controls settings relating to the Java compiler.
class JavaCompiler {
  /// Extra flags to pass to the Java compiler.
  flags: Listing<String> = new {}
}

/// Controls settings relating to the Java language.
class JavaLanguage {
  /// The source version to use.
  source: JvmTarget = "auto"

  /// The release version to use.
  release: JvmTarget = "auto"

  /// Configuration for the Java compiler.
  compiler: JavaCompiler = new {}
}

/// Specifies settings which apply to JVM targets.
class JvmSettings {
  /// Entrypoint class for this project as an application.
  main: JvmClass? = null

  /// Set the JVM bytecode target level for this project.
  target: JvmTarget = "auto"

  /// Set a custom Java Home override for this project.
  javaHome: String? = null

  /// Controls features and settings related to JVM support.
  features: JvmFeatures = new {}

  /// Java language settings.
  java: JavaLanguage = new {}

  /// Runtime JVM flags.
  flags: Listing<String> = new {}
}

/// Options for running JVM tests.
class JvmTesting {
  /// Enable JVM testing with automatic discovery using classpath scanning.
  enabled: Boolean = true

  /// Test engine to be used for JVM tests:
  /// - Elide: use the built-in test engine built on GraalVM's Espresso VM; detects JUnit-compatible tests by scsanning
  ///   the classpath.
  /// - JUnit5: use the JUnit Jupiter engine to run compatible tests detected in the classpath.
  driver: "Elide" | "JUnit" = "Elide"
}
