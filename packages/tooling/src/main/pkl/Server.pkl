/// Describes HTTP serving configuration.
@ModuleInfo { minPklVersion = "0.28.1" }
module elide.server

import "Base.pkl" as _common

/// Describes an SSL certificate loaded from a local file.
class LocalFileCertificate {
  /// Path to a file containing the SSL certificate chain in PEM format.
  certFile: _common.FilePath

  /// Path to a file holding the private key for the certificate in PEM format.
  keyFile: _common.FilePath

  /// Optional password used to unlock the certificate key.
  keyPassphrase: String? = null
}

/// Describes a self-signed SSL certificate generated by Elide at runtime for
/// development and test servers.
class SelfSignedCertificate {
  /// Sets the subject of the certificate, defaults to 'localhost'.
  subject: String?

  /// Sets the start of the certificate's validity period, in seconds before
  /// its creation (e.g., a value of `3600` means 'an hour ago'). Defaults to
  /// 5 minutes.
  notBefore: Int?

  /// Sets the end of the certificate's validity period, in seconds after its
  /// creation (e.g., a value of `3600` means 'in an hour'). Defaults to one
  /// year.
  notAfter: Int?
}

/// Configuration for SSL certificates used by HTTPS and HTTP/3 servers.
typealias SSLCertificate = LocalFileCertificate | SelfSignedCertificate

/// Describes a standard socket address formed by a host name and port.
class SocketAddress {
  /// Host where the server will be bound, defaults to 'localhost'.
  hostname: String?

  /// TCP port where the server will be bound; use 0 to let the OS select an
  // available port at runtime. The default value depends on the service.
  port: Int?
}

/// Describes a Unix domain socket address as an absolute path.
typealias DomainSocketAddress = _common.FilePath

/// Describes an address to which a server application can be bound.
typealias BindingAddress = SocketAddress | DomainSocketAddress

/// Describes a specific server transport implementation.
typealias ServerTransport = "io_uring" | "kqueue" | "epoll" | "nio"

/// Configures the HTTPS features for declarative server apps and static asset
/// servers.
class HttpsServerSettings {
  /// SSL certificate used by the server.
  certificate: SSLCertificate

  /// Address the server will be bound to. Defaults to localhost:8443.
  address: BindingAddress?
}

/// Configures the HTTP/3 feature for declarative server apps and static asset
/// servers.
class Http3ServerSettings {
  /// SSL certificate used by the server.
  certificate: SSLCertificate

  /// Address the server will be bound to. Defaults to localhost:8443.
  ///
  /// Note that HTTP/3 uses UDP instead of TCP; when using a regular socket
  /// address, it is the default to use the same port number as the HTTPS
  /// service.
  address: BindingAddress?

  // Whether to advertise the HTTP/3 service from the cleartext and HTTPS
  // endpoints via 'Alt-Svc' headers. Disabled by default.
  advertise: Boolean = false
}

/// Configures the built-in HTTP engine used for declarative guest server
/// applications (e.g. Workers-style apps, WSGI, static assets).
class HttpServerSettings {
  /// Address the server will be bound to. Defaults to localhost:8080.
  address: BindingAddress?

  /// Whether the default insecure HTTP server is enabled.
  cleartext: Boolean = true

  /// Optional override for the server transport to be used for all services.
  transport: ServerTransport?

  /// Configures an HTTPS service for the application.
  ///
  /// When used alongside a cleartext server (the default), an 'Alt-Svc' header
  /// will be used to advertise protocol upgrades ('H2').
  https: HttpsServerSettings?

  /// Configures an HTTP/3 service for the application.
  http3: Http3ServerSettings?

  /// Custom value for the 'Server' header.
  serverName: String?
}
